plugins {
    id 'java-library'
    id "com.jfrog.bintray" version "1.8.0"
    id 'net.researchgate.release' version '2.7.0'
    id "org.jetbrains.kotlin.jvm" version "1.3.21"
    id 'maven-publish'
}

apply from: "$rootDir/gradle/versioning.gradle"

group = 'org.pgexposed'

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

sourceCompatibility = 1.8
targetCompatibility = 1.8

defaultTasks 'jar'

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url  "https://dl.bintray.com/pgexposed/PgExposed"
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compileOnly 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.0.1'
    compile 'joda-time:joda-time:2.9.9'
    compile 'org.slf4j:slf4j-api:1.7.25'

    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testCompile 'org.slf4j:slf4j-log4j12:1.7.25'
    testCompile 'log4j:log4j:1.2.17'
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.postgresql:postgresql:42.2.5'
    testCompile 'com.opentable.components:otj-pg-embedded:0.12.0'
}

test {
    jvmArgs "-XX:MaxPermSize=256m"
    testLogging {
        events "PASSED", "FAILED", "SKIPPED"
        showStandardStreams = true
        exceptionFormat = 'short'
    }
}

publishing {
    publications {
        pgexposed(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifactId = 'pgexposed'
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    pkg {
        userOrg = 'pgexposed'
        repo = 'pgexposed'
        name = 'pgexposed'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/spolnik/PgExposed.git'
    }
    version {
        name = version
        desc = 'PgExposed - async Kotlin Postgres ORM library'
        released  = new Date()
        vcsTag = version
    }
}

afterReleaseBuild.dependsOn bintrayUpload

wrapper {
    gradleVersion = "5.2.1"
    distributionType = Wrapper.DistributionType.ALL
}
