plugins {
    id 'java-library'
    id "com.jfrog.bintray" version "1.8.4"
    id "org.jetbrains.kotlin.jvm" version "1.3.21"
    id 'maven-publish'
    id "jacoco"
}

apply from: "$rootDir/gradle/versioning.gradle"

group = 'org.pgexposed'

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

sourceCompatibility = 1.8
targetCompatibility = 1.8

defaultTasks 'jar'

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

repositories {
    mavenLocal()
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compileOnly 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.0.1'
    compile 'joda-time:joda-time:2.9.9'
    compile 'org.slf4j:slf4j-api:1.7.25'

    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testCompile 'org.slf4j:slf4j-log4j12:1.7.25'
    testCompile 'log4j:log4j:1.2.17'
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.postgresql:postgresql:42.2.5'
    testCompile 'com.opentable.components:otj-pg-embedded:0.12.0'
}

tasks.withType(JavaCompile) {
    options.incremental = true
}

test {
    testLogging {
        // set options for log level LIFECYCLE
        events "failed"
        exceptionFormat "short"

        // set options for log level DEBUG
        debug {
            events "started", "skipped", "failed"
            exceptionFormat "full"
        }

        // remove standard output/error logging from --info builds
        // by assigning only 'failed' and 'skipped' events
        info.events = ["failed", "skipped"]
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId 'org.pgexposed'
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['mavenJava']
    publish = true

    pkg {
        userOrg = 'pgexposed'
        repo = 'maven'
        name = 'pgexposed'
        desc = 'PgExposed Kotlin SQL Lib'
        websiteUrl = 'https://github.com/spolnik/PgExposed'
        issueTrackerUrl = 'https://github.com/spolnik/PgExposed/issues'
        vcsUrl = 'https://github.com/spolnik/PgExposed.git'
        publicDownloadNumbers = true
        licenses = ['Apache-2.0']
        labels = ['postgres', 'kotlin', 'sql', 'orm']

        version {
            name = "${project.version}"
            desc = "PgExposed Kotlin SQL Lib ${project.version}"
            vcsTag = "${project.version}"
            released = new Date()
        }
    }
}

wrapper {
    gradleVersion = "5.2.1"
    distributionType = Wrapper.DistributionType.ALL
}
